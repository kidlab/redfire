<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:adobe="com.adobe.flex.extras.controls.springgraph.*"
	verticalAlign="middle">
	
	<mx:Script>
		<![CDATA[

			import com.salesbuilder.model.Account;
			import com.salesbuilder.model.Contact;
			import mx.events.CollectionEvent;
			import mx.collections.ArrayCollection;
			import com.adobe.flex.extras.controls.springgraph.Graph;			
			import com.adobe.flex.extras.controls.springgraph.Item;	
			import com.salesbuilder.view.OrgChartItemView;		

			[Bindable] private var _contacts:ArrayCollection;
			[Bindable] public var account:Account;
			
			public function set contacts(contacts:ArrayCollection):void
			{
				_contacts = contacts;
				buildGraph();
				contacts.addEventListener(CollectionEvent.COLLECTION_CHANGE, 
					function(event:Event):void
					{
						buildGraph();
					});
			}

			private var items:Graph;

			private function buildGraph(): void 
			{
				if (_contacts==null) 
				{
					springgraph.dataProvider = null;
					return;				
				}
				
				items = new Graph();
				var contact:Contact
				var item:Item;
				for (var i:int=0; i<_contacts.length; i++)
				{
					contact = _contacts.getItemAt(i) as Contact;
					contact.account = account;
					item = new Item(""+contact.contactId);
					item.data = contact;
					items.add(item);
				}

				var employee:Item;
				var manager:Item;
				for (var j:int=0; j<_contacts.length; j++)
				{
					contact = _contacts.getItemAt(j) as Contact;
					if (contact.managerId > 0)
					{
						employee = items.find(""+contact.contactId);	
						manager = items.find(""+contact.managerId);
						items.link(employee, manager);	
					}
				}
				springgraph.dataProvider = items;
			}
			
		]]>
	</mx:Script>

	<adobe:SpringGraph id="springgraph" width="100%" height="100%"
		repulsionFactor="{repulsion.value}" itemRenderer="com.salesbuilder.view.OrgChartItemView">
	</adobe:SpringGraph>

	<mx:VSlider id="repulsion" value="0.5" minimum="0.0" maximum="1.5" liveDragging="true" width="13" height="150"
	    thumbSkin="@Embed('/assets/thumb.png')"
        trackSkin="@Embed('/assets/track.png')"/> 

</mx:HBox>